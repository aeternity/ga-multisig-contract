// ISC License
//
// Copyright (c) 2022, aeternity developers
//
// Credit to Michel Meier & Marco Walz (kryptokrauts.com)
//
// Permission to use, copy, modify, and/or distribute this software for any
// purpose with or without fee is hereby granted, provided that the above
// copyright notice and this permission notice appear in all copies.
//
// THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
// REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY
// AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
// INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
// LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE
// OR OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
// PERFORMANCE OF THIS SOFTWARE.

// THIS IS NOT SECURITY AUDITED
// DO NEVER USE THIS WITHOUT SECURITY AUDIT FIRST

@compiler >= 6

include "Option.aes"
include "Set.aes"
include "String.aes"

// This contract provides multi signature based on generalized accounts.
// A transaction can be proposed by one of the signers which are registered during contract deployment.
// The transaction must be confirmed by a certain amount of signers. This is also defined during contract deployment.
// Proposing a transaction automatically counts as confirmation.
// When proposing a tx, the signer can define a TTL until the proposed tx needs to be confirmed and authorized.
// If the amount of confirmations is reached, the special function "authorize" can be called by anybody.
// Only one transaction can be handled at the same time.
// Every signer can revoke the proposed transaction at any time (no matter if it's expired or not).
contract SimpleGAMultiSig =

  datatype event
    = TxProposed(hash, address, int)
    | TxConfirmed(hash, address)
    | TxConsensusReached(hash)
    | TxRevoked(hash, address)
    | TxAuthorized(hash)

  record transaction =
    { ga_tx_hash : hash,
      expires_at : int,
      proposed_by : address,
      confirmed_by : Set.set(address) }

  record consensus_info =
    { ga_tx_hash : option(hash),
      confirmations_required : int,
      confirmed_by : list(address),
      has_consensus : bool,
      expired : bool }

  record state =
  	{ signers : Set.set(address),
  	  confirmations_required : int,
      current_tx : option(transaction) }

  stateful entrypoint init(p_confirmations_required : int, p_cosigners : list(address)) =
    { signers = Set.insert(Call.caller, Set.from_list(p_cosigners)),
  	  confirmations_required = p_confirmations_required,
  	  current_tx = None }

  // GA authorize function
  stateful entrypoint authorize() : bool =
    let tx = Option.force_msg(state.current_tx, "ERROR_NO_TX_PROPOSED")
    require(!is_expired(tx), "ERROR_TX_EXPIRED")
    switch(Auth.tx_hash)
      None => abort("ERROR_NO_AUTH_CONTEXT")
      Some(ga_tx_hash) =>
        require(has_consensus(tx), "ERROR_NO_CONSENSUS")
        require(tx.ga_tx_hash == ga_tx_hash, "ERROR_UNEQUAL_HASHES")
        Chain.event(TxAuthorized(ga_tx_hash))
        put(state{ current_tx = None })
        true

  // propose a new tx valid for the given ttl
  stateful entrypoint propose(ga_tx_hash : hash, ttl : Chain.ttl, sig : signature) =
    require_valid_signer_and_sig(Call.caller, ga_tx_hash, sig)
    switch(state.current_tx)
      None => set_current_tx(ga_tx_hash, ttl)
      Some(tx) =>
        require(is_expired(tx), "ERROR_A_TX_IS_ALREADY_PROPOSED")
        set_current_tx(ga_tx_hash, ttl)

  // signer confirms tx
  stateful entrypoint confirm(sig : signature) =
    let tx = Option.force_msg(state.current_tx, "ERROR_NOTHING_TO_CONFIRM")
    require_valid_signer_and_sig(Call.caller, tx.ga_tx_hash, sig)
    require(!Set.member(Call.caller, tx.confirmed_by), "ERROR_ALREADY_CONFIRMED")
    require(!is_expired(tx), "ERROR_TX_ALREADY_EXPIRED")
    let updated_tx = tx{ confirmed_by = Set.insert(Call.caller, tx.confirmed_by) }
    put(state{ current_tx = Some(updated_tx) })
    Chain.event(TxConfirmed(tx.ga_tx_hash, Call.caller))
    if(has_consensus(updated_tx))
      Chain.event(TxConsensusReached(tx.ga_tx_hash))

  // revoke the current tx and clean state
  stateful entrypoint revoke(sig : signature) =
    let tx = Option.force_msg(state.current_tx, "ERROR_NOTHING_TO_REVOKE")
    require_valid_signer_and_sig(Call.caller, tx.ga_tx_hash, sig)
    put(state{ current_tx = None })
    Chain.event(TxRevoked(tx.ga_tx_hash, Call.caller))

  // return all signers
  entrypoint get_signers() : list(address) =
  	Set.to_list(state.signers)

  // return the consensus info
  entrypoint get_consensus_info() : consensus_info =
    switch(state.current_tx)
      None => { ga_tx_hash = None,
                confirmations_required = state.confirmations_required,
                confirmed_by = [],
                has_consensus = false,
                expired = false }
      Some(tx) => { ga_tx_hash = Some(tx.ga_tx_hash),
                    confirmations_required = state.confirmations_required,
                    confirmed_by = Set.to_list(tx.confirmed_by),
                    has_consensus = has_consensus(tx),
                    expired = is_expired(tx) }

  // validate if enough signers have confirmed
  function has_consensus(tx: transaction) : bool =
    Set.size(tx.confirmed_by) >= state.confirmations_required

  // require that given address is within the list of signers
  // AND
  // that the signature of the proposed ga tx was made using the private key of the given address
  function require_valid_signer_and_sig(addr : address, ga_tx_hash_to_verify : hash, sig : signature) =
    require(Set.member(addr, state.signers) && Crypto.verify_sig(ga_tx_hash_to_verify, addr, sig), "ERROR_NOT_AUTHORIZED")

  // check if current_tx is already expired
  function is_expired(tx: transaction) : bool =
    Chain.block_height > tx.expires_at

  // set current tx to state and emit new proposed event
  stateful function set_current_tx(p_ga_tx_hash : hash, ttl : Chain.ttl) =
    let new_tx = { ga_tx_hash = p_ga_tx_hash,
                   expires_at = expiration_height(ttl),
                   proposed_by = Call.caller,
                   confirmed_by = Set.insert(Call.caller, Set.new()) }
    put(state{ current_tx = Some(new_tx) })
    Chain.event(TxProposed(new_tx.ga_tx_hash, new_tx.proposed_by, new_tx.expires_at))

  // compute the expiration height depending on the ttl type
  function expiration_height(ttl : Chain.ttl) : int =
    switch(ttl)
      FixedTTL(height) => height
      RelativeTTL(relative_ttl) => Chain.block_height + relative_ttl